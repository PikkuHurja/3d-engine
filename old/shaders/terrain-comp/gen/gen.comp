#version 430
layout(local_size_x = 16, local_size_y = 16) in;

/*
    heightmap needs to have overscan, so that it matches the ting, not sure how to impl :3
*/
layout(binding = 0) uniform sampler2D height_map;
uniform uint    access_offset           = 0;         //offset for when generating multiple chunks, the unit is per chunck
uniform ivec2   chunck_position         = ivec2(0);
uniform uint    vertex_count            = 32; //[0, 1] (chunk should include these verticies, yes there are double verticies, but its a price im willing to pay rn)
uniform float   chunck_size             = 32;
uniform float   height_map_strenght     = 10;

    //ye we doing the fucky wucky with this one.... might as well kms ig
const uint stride           = 4*3 + 2;
const uint position_offset  = 0;
const uint uv_offset        = 3;
const uint normal_offset    = 5;
const uint tangent_offset   = 8;
const uint bitangent_offset = 11;
layout(std430, binding = 0) writeonly buffer chunk{
    float chunk_data[];
};

/*
vec3 aVertex
vec3 aNormal
vec3 aTangent
vec3 aBitangent

vec2 aUV; //will also store the world coordinates
*/


/// PERLIN FRACTAL NOISE

uniform uint    noise_seed          = 0;
uniform float   noise_frequency     = 0.2;
uniform int     noise_octave_count  = 12;
uniform float   noise_persistence   = 0.5;
uniform float   noise_lacunarity    = 2.0;

// implementation of MurmurHash (https://sites.google.com/site/murmurhash/) for a 
// single unsigned integer.

uint hash(uint x, uint noise_seed) {
    const uint m = 0x5bd1e995U;
    uint hash = noise_seed;
    // process input
    uint k = x;
    k *= m;
    k ^= k >> 24;
    k *= m;
    hash *= m;
    hash ^= k;
    // some final mixing
    hash ^= hash >> 13;
    hash *= m;
    hash ^= hash >> 15;
    return hash;
}

// implementation of MurmurHash (https://sites.google.com/site/murmurhash/) for a  
// 2-dimensional unsigned integer input vector.

uint hash(uvec2 x, uint noise_seed){
    const uint m = 0x5bd1e995U;
    uint hash = noise_seed;
    // process first vector element
    uint k = x.x; 
    k *= m;
    k ^= k >> 24;
    k *= m;
    hash *= m;
    hash ^= k;
    // process second vector element
    k = x.y; 
    k *= m;
    k ^= k >> 24;
    k *= m;
    hash *= m;
    hash ^= k;
	// some final mixing
    hash ^= hash >> 13;
    hash *= m;
    hash ^= hash >> 15;
    return hash;
}

vec2 gradientDirection(uint hash) {
    switch (int(hash) & 3) { // look at the last two bits to pick a gradient direction
    case 0:
        return vec2(1.0, 1.0);
    case 1:
        return vec2(-1.0, 1.0);
    case 2:
        return vec2(1.0, -1.0);
    case 3:
        return vec2(-1.0, -1.0);
    }
}

float interpolate(float value1, float value2, float value3, float value4, vec2 t) {
    return mix(mix(value1, value2, t.x), mix(value3, value4, t.x), t.y);
}

vec2 fade(vec2 t) {
    // 6t^5 - 15t^4 + 10t^3
	return t * t * t * (t * (t * 6.0 - 15.0) + 10.0);
}

float perlinNoise(vec2 position, uint noise_seed) {
    vec2 floorPosition = floor(position);
    vec2 fractPosition = position - floorPosition;
    uvec2 cellCoordinates = uvec2(floorPosition);
    float value1 = dot(gradientDirection(hash(cellCoordinates, noise_seed)), fractPosition);
    float value2 = dot(gradientDirection(hash((cellCoordinates + uvec2(1, 0)), noise_seed)), fractPosition - vec2(1.0, 0.0));
    float value3 = dot(gradientDirection(hash((cellCoordinates + uvec2(0, 1)), noise_seed)), fractPosition - vec2(0.0, 1.0));
    float value4 = dot(gradientDirection(hash((cellCoordinates + uvec2(1, 1)), noise_seed)), fractPosition - vec2(1.0, 1.0));
    return interpolate(value1, value2, value3, value4, fade(fractPosition));
}

float perlinNoise(vec2 position) {
    float value = 0.0;
    float amplitude = 1.0;
    float currentFrequency = noise_frequency;
    uint currentSeed = noise_seed;
    for (int i = 0; i < noise_octave_count; i++) {
        currentSeed = hash(currentSeed, 0x0U); // create a new noise_seed for each octave
        value += perlinNoise(position * currentFrequency, currentSeed) * amplitude;
        amplitude *= noise_persistence;
        currentFrequency *= noise_lacunarity;
    }
    return value;
}


float calculate_height(vec2 world_xy){
    return perlinNoise(world_xy)*height_map_strenght;
}


void main(){
    uvec2 l = gl_GlobalInvocationID.xy;
    if(l.x >= vertex_count || l.y >= vertex_count) return;

        // the definition of shenanigans
    vec2 uv         = vec2(l)/vec2(vertex_count-1);

    vec2 chunck_sign = vec2(
        chunck_position.x < 0.0 ? -1.0 : 1.0,
        chunck_position.y < 0.0 ? -1.0 : 1.0
    );
    vec2 global_uv      = (uv + abs(chunck_position)) * chunck_sign;

    vec2    xz_position     = global_uv*chunck_size;
    float   y_position      = calculate_height(global_uv);
    uint    offset          = (access_offset*vertex_count*vertex_count + l.x + l.y * vertex_count)*stride;
    
    chunk_data[offset+position_offset]      = xz_position.x;
    chunk_data[offset+position_offset+1]    = y_position;
    chunk_data[offset+position_offset+2]    = xz_position.y;

    chunk_data[offset+uv_offset]    =  global_uv.x;
    chunk_data[offset+uv_offset+1]  =  global_uv.y;

    /*
    const float delta =  1.f/(vertex_count-1.f);
    float extra_samples[4];
    extra_samples[0] = calculate_height(global_uv-vec2(delta, 0));
    extra_samples[1] = calculate_height(global_uv+vec2(delta, 0));
    extra_samples[2] = calculate_height(global_uv-vec2(0, delta));
    extra_samples[3] = calculate_height(global_uv+vec2(0, delta));

    vec3 dx = vec3(2.0 * delta * chunck_size, extra_samples[1] - extra_samples[0], 0.0);
    vec3 dz = vec3(0.0, extra_samples[3] - extra_samples[2], dx.x);
    
        //for some reason, this calculates the opposite
    vec3 normal = normalize(cross(dx,dz));
    vec3 tangent = normalize(dx);
    vec3 bitangent = normalize(dz);
    */

    const float delta =  1.f/(vertex_count-1.f);
    float x_delta = calculate_height(global_uv+vec2(delta, 0));
    float y_delta = calculate_height(global_uv+vec2(0, delta));

    //extra_samples[0] = calculate_height(global_uv-vec2(delta, 0));
    //extra_samples[1] = calculate_height(global_uv+vec2(delta, 0));
    //extra_samples[2] = calculate_height(global_uv-vec2(0, delta));
    //extra_samples[3] = calculate_height(global_uv+vec2(0, delta));

    vec3 dx = vec3(delta * chunck_size, x_delta-y_position , 0.0);
    vec3 dz = vec3(0.0, y_delta-y_position, dx.x);
    
        //for some reason, this calculates the opposite
        //so we negate it
    vec3 normal     = -normalize(cross(dx,dz));
    vec3 tangent    = -normalize(dx);
    vec3 bitangent  = -normalize(dz);
    
    chunk_data[offset+normal_offset]        = normal.x;
    chunk_data[offset+normal_offset+1]      = normal.y;
    chunk_data[offset+normal_offset+2]      = normal.z;

    chunk_data[offset+tangent_offset]       = tangent.x;
    chunk_data[offset+tangent_offset+1]     = tangent.y;
    chunk_data[offset+tangent_offset+2]     = tangent.z;

    chunk_data[offset+bitangent_offset]     = bitangent.x;
    chunk_data[offset+bitangent_offset+1]   = bitangent.y;
    chunk_data[offset+bitangent_offset+2]   = bitangent.z;

}